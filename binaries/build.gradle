apply plugin: 'java'

configurations {
    jni
}

task copyNatives(type: Copy) {
    outputs.dir file("${buildDir}/resources/main")

    project.afterEvaluate {
        configurations.jni.files.each { File file ->
            def binaryMatch = file.name =~ /.*\.(so|dll|dylib)/
            if (binaryMatch) {
                inputs.file file
                from file
            }
        }
    }

    into project.file("$buildDir/resources/main")
}

jar {
    dependsOn copyNatives
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.almworks.sqlite4java:sqlite4java:1.0.392'
    implementation 'com.google.guava:guava:28.0-android'

    jni 'com.almworks.sqlite4java:libsqlite4java-osx:1.0.392'
    jni 'com.almworks.sqlite4java:libsqlite4java-linux-amd64:1.0.392'
    jni 'com.almworks.sqlite4java:libsqlite4java-linux-i386:1.0.392'
    jni 'com.almworks.sqlite4java:sqlite4java-win32-x64:1.0.392'
    jni 'com.almworks.sqlite4java:sqlite4java-win32-x86:1.0.392'
}

